Low Ceramic Sardine

medium

# Users depositing before farming starts can accrue points

## Summary
The `SophonFarming` has a start day which defines at which block farming can start. If the contract owner updates the `startBlock` using `setStartBlock()` users who deposit their assets before that block and after the previously set `startBlock` accrue points. The points accrual should happen only after the new `startBlock` value.
## Vulnerability Detail
When `SophonFarming` is initialized using `initialize()` three pools are created (sDAI, wstETH and weETH) using `add()`. `add()` creates the `PoolInfo` data structure and sets the `PoolInfo.lastRewardBlock` effectively to the `startBlock` (if it is higher than the current block). `startBlock` is set during `initialize()`. The `PoolInfo.lastRewardBlock` controls when the points could start accruing. Accrual shall happen only after that block.

If the protocol team decides to change the the `startBlock` using `setStartBlock()` it will update the global variable `startBlock`, but the pool's `lastRewardBlock` will be unaffected, hence users could still accrue points after the old value and before the new.

The following PoC presents it:
```solidity
    function test_earlyDeposit() public {

        uint256 startingBlock = block.number + 20;
        vm.startPrank(deployer);
        sophonFarming.setStartBlock(startingBlock);
        sophonFarming.setEndBlock(startingBlock + 100, 50);
        vm.stopPrank();

        uint256 amountToDeposit1 = 10e18;
        uint256 poolId1 = sophonFarming.typeToId(SophonFarmingState.PredefinedPool.wstETH);
       
        // deposit 1, 20 blocks before the start
        vm.startPrank(account1);
        deal(address(wstETH), account1, amountToDeposit1);
        wstETH.approve(address(sophonFarming), amountToDeposit1);
        sophonFarming.deposit(poolId1, amountToDeposit1, 0);
        vm.stopPrank();

        vm.roll(startingBlock);

        // deposit 2 on the starting block.
        vm.startPrank(account2);
        deal(address(wstETH), account2, amountToDeposit1);

        wstETH.approve(address(sophonFarming), amountToDeposit1);
        sophonFarming.deposit(poolId1, amountToDeposit1, 0);
        vm.stopPrank();

        vm.roll(startingBlock + 10);

        sophonFarming.updatePool(poolId1);

        console.log("points user1: %d", sophonFarming.pendingPoints(poolId1, account1));
        console.log("points user2: %d", sophonFarming.pendingPoints(poolId1, account2));

    }
```
The PoC output shows that user1 was able to accrue more points than user2 (who deposited at the new `startBlock`):
```jsx
Logs:
  points user1: 208333333333333333320
  points user2: 41666666666666666660
```

## Impact
Malicious users depositing earlier than other users accrue more points and hence gain unfair advantage.

## Code Snippet
Where the `lastRewardBlock` is set for a pool.
https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L178
`setStartBlock()` - the problematic function:
https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L272C14-L272C15

## Tool used
Manual Review

## Recommendation
The recommend way would be add pool updates the `setStartBlock()`:
```diff
    function setStartBlock(uint256 _startBlock) public onlyOwner {
        if (_startBlock == 0 || (endBlock != 0 && _startBlock >= endBlock)) {
            revert InvalidStartBlock();
        }
        if (getBlockNumber() > startBlock) {
            revert FarmingIsStarted();
        }
        startBlock = _startBlock;
+        uint256 length = poolInfo.length;
+        for (uint256 i = 0; i < length; i++) {
+            poolInfo[i].lastRewardBlock = _startBlock;
+        }
    }
```

