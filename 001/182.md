Fluffy Midnight Mallard

medium

# Insufficient Precision in `accPointsPerShare`


## Summary
The precision of `accPointsPerShare` is not adequate.
The total points distributed to depositors should equal pointsPerBlock * (blockNumber - startBlock).
However, due to the limited precision of `accPointsPerShare`, the total points distributed may be less than this expected value.
The protocol anticipates an error margin of 1 wei per user, but this margin can exceed 1 wei if the pool amount is large.

## Vulnerability Detail

```solidity
File: contracts/farm/SophonFarming.sol#L279
    function updatePool(uint256 _pid) public {
        PoolInfo storage pool = poolInfo[_pid];
        if (getBlockNumber() <= pool.lastRewardBlock) {
            return;
        }
        uint256 lpSupply = pool.amount;
        uint256 _pointsPerBlock = pointsPerBlock;
        uint256 _allocPoint = pool.allocPoint;
        if (lpSupply == 0 || _pointsPerBlock == 0 || _allocPoint == 0) {
            pool.lastRewardBlock = getBlockNumber();
            return;
        }
        uint256 blockMultiplier = _getBlockMultiplier(pool.lastRewardBlock, getBlockNumber());
        uint256 pointReward =
            blockMultiplier * // @audit-info 1e18
            _pointsPerBlock * // 25e18
            _allocPoint /     // 20,000
            totalAllocPoint;  // 60,000

        pool.accPointsPerShare = pointReward / //@audit here
            lpSupply +
            pool.accPointsPerShare;

        pool.lastRewardBlock = getBlockNumber();
    }
File: contracts/farm/SophonFarming.sol#L163
    function test_MultipleDeposits(uint256 rollBlocks) public {
        vm.assume(rollBlocks > 0 && rollBlocks < 1e18);
        setOneDepositorPerPool();
        SophonFarmingState.UserInfo[][] memory userInfos =
            new SophonFarmingState.UserInfo[][](sophonFarming.getPoolInfo().length);

        address[] memory accounts = new address[](3);
        accounts[0] = account1;
        accounts[1] = account2;
        accounts[2] = account3;

        uint256 poolsLength = sophonFarming.poolLength();

        vm.roll(block.number + rollBlocks);

        sophonFarming.massUpdatePools();

        uint256[][] memory pendingPoints = new uint256[][](poolsLength);
        pendingPoints = sophonFarming.getPendingPoints(accounts);

        uint256[4][][] memory optimizedUserInfos = new uint256[4][][](2);
        optimizedUserInfos = sophonFarming.getOptimizedUserInfo(accounts);

        uint256 totalPoints;
        for (uint256 i = 0; i < accounts.length; i++) {
            for (uint256 j = 0; j < poolsLength; j++) {
                SophonFarmingState.UserInfo memory userInfo = getUserInfo(j, accounts[i]);

                totalPoints += pendingPoints[i][j];
                console.log("pendingPoints[i][j]", pendingPoints[i][j]);

                assertEq(userInfo.amount, optimizedUserInfos[i][j][0]);
                assertEq(userInfo.boostAmount, optimizedUserInfos[i][j][1]);
                assertEq(userInfo.depositAmount, optimizedUserInfos[i][j][2]);
                assertEq(pendingPoints[i][j], optimizedUserInfos[i][j][3]);
                assertEq(pendingPoints[i][j], sophonFarming.pendingPoints(j, accounts[i]));
            }
        }

        // Margin of error is 1 wei per user.
        assertApproxEqAbs(totalPoints, pointsPerBlock * rollBlocks, accounts.length);
    }
    [FAIL. Reason: assertion failed: 457799999999999999994575 !~= 457800000000000000000000 (max delta: 3, real delta: 5425); counterexample: calldata=0x44a3265d0000000000000000000000000000000000000000000000000000000000004788 args=[18312 [1.831e4]]] test_MultipleDeposits(uint256) (runs: 0, Î¼: 0, ~: 0)
```

## Impact
The expected `pointsPerBlock` may not be fully distributed due to the insufficient precision of `accPointsPerShare`.


## Code Snippet
[contracts/farm/SophonFarming.sol#L429](https://github.com/sherlock-audit/2024-05-sophon/blob/main/farming-contracts/contracts/farm/SophonFarming.sol#L429)

## Tool used

Manual Review

## Recommendation
It is recommended to multiply `pointReward` by `* 1e18` when calculating `accPointsPerShare` and update all related calculations accordingly.

```diff
File: contracts/farm/SophonFarming.sol#L410
    function updatePool(uint256 _pid) public {
        PoolInfo storage pool = poolInfo[_pid];
        if (getBlockNumber() <= pool.lastRewardBlock) {
            return;
        }
        uint256 lpSupply = pool.amount;
        uint256 _pointsPerBlock = pointsPerBlock;
        uint256 _allocPoint = pool.allocPoint;
        if (lpSupply == 0 || _pointsPerBlock == 0 || _allocPoint == 0) {
            pool.lastRewardBlock = getBlockNumber();
            return;
        }
        uint256 blockMultiplier = _getBlockMultiplier(pool.lastRewardBlock, getBlockNumber());
        uint256 pointReward =
            blockMultiplier * 
            _pointsPerBlock * 
+           1e18 *
            _allocPoint /     
            totalAllocPoint;  

        pool.accPointsPerShare = pointReward /
            lpSupply +
            pool.accPointsPerShare;

        pool.lastRewardBlock = getBlockNumber();
    }
```
