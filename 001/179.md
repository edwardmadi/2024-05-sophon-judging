Mini Daisy Blackbird

medium

# Precision loss leads to incorrect calculation in `_deposit()`


## Summary
Precision loss leads to incorrect calculation in `_deposit()`
## Vulnerability Detail
The division in the following code will cause precision loss and might result in (userAmount * pool.accPointsPerShare / 1e18) being zero.
```javascript
    function _deposit(uint256 _pid, uint256 _depositAmount, uint256 _boostAmount) internal {
        if (isFarmingEnded()) {
            revert FarmingIsEnded();
        }
        if (_depositAmount == 0) {
            revert InvalidDeposit();
        }
        if (_boostAmount > _depositAmount) {
            revert BoostTooHigh(_depositAmount);
        }

        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];
        updatePool(_pid);

        uint256 userAmount = user.amount;
@>        user.rewardSettled =
            userAmount *
            pool.accPointsPerShare /
            1e18 +
            user.rewardSettled -
            user.rewardDebt;

        // booster purchase proceeds
        heldProceeds[_pid] = heldProceeds[_pid] + _boostAmount;

        // deposit amount is reduced by amount of the deposit to boost
        _depositAmount = _depositAmount - _boostAmount;

        // set deposit amount
        user.depositAmount = user.depositAmount + _depositAmount;
        pool.depositAmount = pool.depositAmount + _depositAmount;

        // apply the boost multiplier
        _boostAmount = _boostAmount * boosterMultiplier / 1e18;

        user.boostAmount = user.boostAmount + _boostAmount;
        pool.boostAmount = pool.boostAmount + _boostAmount;

        // userAmount is increased by remaining deposit amount + full boosted amount
        userAmount = userAmount + _depositAmount + _boostAmount;

        user.amount = userAmount;
        pool.amount = pool.amount + _depositAmount + _boostAmount;

@>        user.rewardDebt = userAmount *
            pool.accPointsPerShare /
            1e18;

        emit Deposit(msg.sender, _pid, _depositAmount, _boostAmount);
    }
```
#### POC
Assume pointsPerBlock is 1000_000_000, and 1_000_000_000e18 DAI has already been staked. user2 deposit 0.1e18 Dai
Add the test function in SophonFarming.t.sol. 
```javascript
function testDepositDai_NotBoostedDepositPrecisionLoss() public {
        vm.prank(deployer);
        sophonFarming.setPointsPerBlock(1000000000);
        uint256 amountToDeposit = 1_00_000_000e18;
        uint256 amountToDeposit2 = 1e17;

        deal(address(dai), account1, amountToDeposit);
        assertEq(dai.balanceOf(account1), amountToDeposit);

        uint256 poolId = sophonFarming.typeToId(SophonFarmingState.PredefinedPool.sDAI);
        //user1
        vm.startPrank(account1);
        dai.approve(address(sophonFarming), amountToDeposit);
        sophonFarming.depositDai(amountToDeposit, 0);
        assertEq(dai.balanceOf(account1), 0);
        vm.stopPrank();

        //user2
        deal(address(dai), account2, amountToDeposit2);
        vm.startPrank(account2);
        dai.approve(address(sophonFarming), amountToDeposit);
        sophonFarming.depositDai(amountToDeposit2, 0);
        assertEq(dai.balanceOf(account2), 0);
        vm.stopPrank();

        SophonFarmingState.UserInfo memory userInfo;
        (userInfo.amount, userInfo.boostAmount, userInfo.depositAmount, userInfo.rewardSettled, userInfo.rewardDebt) =
            sophonFarming.userInfo(poolId, account1);

        assertEq(userInfo.amount, sDAI.convertToShares(amountToDeposit));
        assertEq(userInfo.boostAmount, 0);
        assertEq(userInfo.depositAmount, sDAI.convertToShares(amountToDeposit));
        assertEq(userInfo.rewardSettled, 0);
        assertEq(userInfo.rewardDebt, 0);

        vm.roll(block.number  + 1);
        deal(address(dai), account2, amountToDeposit2);
        //deposit again,to update pool
        vm.startPrank(account2);
        dai.approve(address(sophonFarming), amountToDeposit);
        sophonFarming.depositDai(amountToDeposit2, 0);
        assertEq(dai.balanceOf(account2), 0);
        vm.stopPrank();

        SophonFarmingState.UserInfo memory userInfo2;
        (userInfo2.amount, userInfo2.boostAmount, userInfo2.depositAmount, userInfo2.rewardSettled, userInfo2.rewardDebt) =
            sophonFarming.userInfo(poolId, account2);

        assertEq(userInfo2.amount, sDAI.convertToShares(amountToDeposit2)*2);
        assertEq(userInfo2.boostAmount, 0);
        assertEq(userInfo2.depositAmount, sDAI.convertToShares(amountToDeposit2)*2);
        
        //@audit precision loss lead rewardSettled to zero
        assertEq(userInfo2.rewardSettled, 0);
        assertEq(userInfo2.rewardDebt, 0);

  
    }
```
Then run `forge test --mt testDepositDai_NotBoostedDepositPrecisionLoss -vv` , we will get:

```bash
Ran 1 test for test/SophonFarming.t.sol:SophonFarmingTest
[PASS] testDepositDai_NotBoostedDepositPrecisionLoss() (gas: 793826)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 6.92ms (1.31ms CPU time)
```

## Impact
This will result in a reduction of the reward points users can receive.
## Code Snippet
https://github.com/sherlock-audit/2024-05-sophon/blob/main/farming-contracts/contracts/farm/SophonFarming.sol#L574C1-L624C6

## Tool used

Manual Review

## Recommendation
Set a relatively high precision for the points.
