Spicy Seafoam Osprey

medium

# In `SophonFarming` the functions `add()` and `set()` can cause incorrect distribution and calculations for pool rewards

## Summary
The functions `add()` and `set()` should always call `massUpdatePools()` to update all pools.
## Vulnerability Detail
The issue is that `totalAllocPoint` is used when calculating portion of the reward each pool will get from the total reward and it is one of the main factor when using to calculate the rewards. So when the `totalAllocPoint` is changed without updating pending reward first, then the reward for each pool can be calculated incorrectly. For example if `_withUpdate` is `false` the variable `totalAllocPoint` can be changed without updating the rewards, because it will not call `massUpdatePools()`.
## Impact
Incorrect distribution and calculations for pool rewards.
## Code Snippet
https://github.com/sherlock-audit/2024-05-sophon/blob/main/farming-contracts/contracts/farm/SophonFarming.sol#L153C1-L188C1
```javascript
    function add(uint256 _allocPoint, address _lpToken, string memory _description, bool _withUpdate) public onlyOwner returns (uint256) {
        if (poolExists[_lpToken]) {
            revert PoolExists();
        }
        if (isFarmingEnded()) {
            revert FarmingIsEnded();
        }
        if (_withUpdate) {
            massUpdatePools();
        }
        uint256 lastRewardBlock =
            getBlockNumber() > startBlock ? getBlockNumber() : startBlock;
        totalAllocPoint = totalAllocPoint + _allocPoint;
        poolExists[_lpToken] = true;


        uint256 pid = poolInfo.length;


        poolInfo.push(
            PoolInfo({
                lpToken: IERC20(_lpToken),
                l2Farm: address(0),
                amount: 0,
                boostAmount: 0,
                depositAmount: 0,
                allocPoint: _allocPoint,
                lastRewardBlock: lastRewardBlock,
                accPointsPerShare: 0,
                description: _description
            })
        );


        emit Add(_lpToken, pid, _allocPoint);


        return pid;
    }

```

https://github.com/sherlock-audit/2024-05-sophon/blob/main/farming-contracts/contracts/farm/SophonFarming.sol#L195C1-L216C6
```javascript
    function set(uint256 _pid, uint256 _allocPoint, bool _withUpdate) public onlyOwner {
        if (isFarmingEnded()) {
            revert FarmingIsEnded();
        }
        if (_withUpdate) {
            massUpdatePools();
        }


        PoolInfo storage pool = poolInfo[_pid];
        address lpToken = address(pool.lpToken);
        if (lpToken == address(0) || !poolExists[lpToken]) {
            revert PoolDoesNotExist();
        }
        totalAllocPoint = totalAllocPoint - pool.allocPoint + _allocPoint;
        pool.allocPoint = _allocPoint;


        if (getBlockNumber() < pool.lastRewardBlock) {
            pool.lastRewardBlock = startBlock;
        }


        emit Set(lpToken, _pid, _allocPoint);
    }
```
## Tool used

Manual Review

## Recommendation
One solution is to remove the `_withUpdate` in the `add()` and `set()` and always call the `massUpdatePools()` before changing `totalAllocPoint`  variable.