Witty Bubblegum Leopard

high

# Pool’s point computation will be wrong because `setStartBlock` does not update pool’s `lastRewardBlock`

## Summary
The setStartBlock does not update pool’s lastRewardBlock data. This will lead to issues when the start block is changed but farming has yet not started.

## Vulnerability Detail

`setStartBlock` serves as a way to set a new value for `startBlock`:

```solidity
// File: SophonFarming.sol

function setStartBlock(uint256 _startBlock) public onlyOwner {
        if (_startBlock == 0 || (endBlock != 0 && _startBlock >= endBlock)) {
            revert InvalidStartBlock();
        }
        if (getBlockNumber() > startBlock) {
            revert FarmingIsStarted();
        }
        startBlock = _startBlock;
    }
```

When pools are added to the protocol, their `lastRewardBlock` field is set to the current configured `startBlock` if its value is greater than the timestamp (meaning currently start period has not been reached):

```solidity
// File: SophonFarming.sol

function add(uint256 _allocPoint, address _lpToken, string memory _description, bool _withUpdate) public onlyOwner returns (uint256) {
        ...
        
        uint256 lastRewardBlock =
            getBlockNumber() > startBlock ? getBlockNumber() : startBlock;
       
	     ...

        poolInfo.push(
            PoolInfo({
                lpToken: IERC20(_lpToken),
                l2Farm: address(0),
                amount: 0,
                boostAmount: 0,
                depositAmount: 0,
                allocPoint: _allocPoint,
                lastRewardBlock: lastRewardBlock,
                accPointsPerShare: 0,
                description: _description
            })
        );

        emit Add(_lpToken, pid, _allocPoint);

        return pid;
    }

```

The problem is that the `setStartBlock` should update each already-added pool’s `lastRewardBlock` to the newly-configured start block, given that otherwise the pools will remain with the same old start block value, when in reality they should have the newly-configured one.

Because pools will remain with the old `lastRewardBlock` value configured, initial pool updates will make the `blockMultiplier` increase, given that they will consider the difference between the current timestamp and the wrongly set `lastRewardBlock`, instead of considering the difference between the current timestamp and the newly-set `startBlock`.

## Impact

High. Point computations will be performed having a wrong start block as reference.

## Proof of Concept

Let’s say the protocol initially has configured a `startBlock` with value 15.

A new pool is then added to the protocol via `add`, so its configured `lastRewardBlock` is set to 15.

After some time (and prior to reaching timestamp 15), the protocol decides to delay the farming start, and calls `setStartBlock` with a new start block parameter of 20.

Although the newly globally-configured `startBlock` is now set to 20, the pool added previously will still have a `lastRewardBlock` of 15. This means that when farming starts at timestamp 15, the `updatePool` function that will be triggered will compute the `blockMultiplier` as the difference between the current timestamp and timestamp 15, not 20. This will result in an increased `blockMultiplier`, and as a result, in a wrongly-increased amount of point rewards for the users of that pool:

```solidity
// File: SophonFarming.sol

function updatePool(uint256 _pid) public {
        PoolInfo storage pool = poolInfo[_pid];
        ...
        uint256 blockMultiplier = _getBlockMultiplier(pool.lastRewardBlock, getBlockNumber());
        uint256 pointReward =
            blockMultiplier *
            _pointsPerBlock *
            _allocPoint /
            totalAllocPoint;

        ...
    }
```

## Code Snippet

https://github.com/sherlock-audit/2024-05-sophon/blob/main/farming-contracts/contracts/farm/SophonFarming.sol#L272-L279

## Tool used

Manual Review

## Recommendation

Update each configured pool’s lastRewardBlock when calling the setStartBlock function to the newly-configured start block. 