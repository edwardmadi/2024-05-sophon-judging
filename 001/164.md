Proper Admiral Dalmatian

medium

# Improper decimal handling in `SophonFarming::massUpdatePools()` and `SophonFarming::_pendingPoints()` could lead to incorrect reward calculation

## Summary

The `SophonFarming` contract utilizes the staking logic from the well-known MasterChef contract. However, the `updatePool()` and `_pendingPoints()` functions do not handle the decimal points correctly, which could lead to incorrect reward calculations.

## Vulnerability Detail

https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L430
https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L361
https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L373
https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L379

The `SophonFarming` contract utilizes the `accPointsPerShare` variable to calculate the rewards for each user per pool. The variable is set to be multiplied by `1e18` as per the documentation:

> uint256 accPointsPerShare; // Accumulated points per share, times 1e18. See below.

However, in the current implementation, the above is not properly applied to all cases, leading to incorrect reward calculations.

## Impact

This vulnerability could lead to improper reward calculation, which could result in a loss of funds for the users.

## Code Snippet

```solidity
function updatePool(uint256 _pid) public {
    PoolInfo storage pool = poolInfo[_pid];
    if (getBlockNumber() <= pool.lastRewardBlock) {
        return;
    }
    uint256 lpSupply = pool.amount;
    uint256 _pointsPerBlock = pointsPerBlock;
    uint256 _allocPoint = pool.allocPoint;
    if (lpSupply == 0 || _pointsPerBlock == 0 || _allocPoint == 0) {
        pool.lastRewardBlock = getBlockNumber();
        return;
    }
    uint256 blockMultiplier = _getBlockMultiplier(pool.lastRewardBlock, getBlockNumber());
    uint256 pointReward =
        blockMultiplier *
        _pointsPerBlock *
        _allocPoint /
        totalAllocPoint;

@>    pool.accPointsPerShare = pointReward / // `pointReward` needs to be multiplied by 1e18
                lpSupply +
                    pool.accPointsPerShare;

    pool.lastRewardBlock = getBlockNumber();
}
```


```solidity
    function _pendingPoints(uint256 _pid, address _user) internal view returns (uint256) {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][_user];

@>        uint256 accPointsPerShare = pool.accPointsPerShare * 1e18; // `accPointsPerShare` is already multiplied by 1e18 by default so this is not needed

        uint256 lpSupply = pool.amount;
        if (getBlockNumber() > pool.lastRewardBlock && lpSupply != 0) {
            uint256 blockMultiplier = _getBlockMultiplier(pool.lastRewardBlock, getBlockNumber());

            uint256 pointReward =
                blockMultiplier *
                pointsPerBlock *
                pool.allocPoint /
                totalAllocPoint;

            accPointsPerShare = pointReward * 
@>                1e18 / // here we have multiplication by 1e18, which if combined with the above line, will lead to incorrect reward calculation
                lpSupply +
                accPointsPerShare;
        }

        return user.amount *
            accPointsPerShare /
@>            1e36 + // should be diveded by 1e18 as this is the actual precision multiplier
            user.rewardSettled -
            user.rewardDebt;
    }
```

## Tool used

Manual Review

## Proof of concept

1. Let's assume that Alice is the first depositor in pool 1 with `1e18` LP tokens.
2. The first stop will be the `updatePool()`.
3. The `blockMultiplier` will be `1e18`, `pointsPerBlock = 25e18`, `allocPoint = 20000` and `totalAllocPoint = 20000`.
4. The `pointReward` will be `1e18 * 25e18 * 20000 / 20000 = 25e36`.
5. Now the `pool.accPointsPerShare` will be `25e36 / 1e18 = 25e18`.
6. Alice wants to check her pending rewards and calls `_pendingPoints()`.
7. Now the `accPointsPerShare` will be `25e18 * 1e18 = 25e36`.
8. A new `pointReward` will be calculated as `1e18 * 25e18 * 20000 / 20000 = 25e36`.
9. The new `accPointsPerShare` will be `25e36 * 1e18 / 1e18 = 25e36` and will be added to the previous one.
10. We already have an enormous decimal representation difference and the second calculation of `accPointsPerShare` will be incorrect.
11. The `pendingPoints` will be calculated as `1e18 * 25e36 / 1e36 = 25e18`, which is incorrect.

If we now apply the MasterChef logic:
1.The `pointReward` will be `1e18 * 25e18 * 20000 / 20000 = 25e36`
2. The `accPointsPerShare` will be `25e36 * 1e18 / 1e18 = 25e36`
3. We now go to the `pendingPoints` calculation:
4. We do not multiply by an additional `1e18`
5. There is a new reward calculation `1e18 * 25e18 * 20000 / 20000 = 25e36`.
6. The new `accPointsPerShare` will be `25e36 * 1e18 / 1e18 = 25e36` and will be added to the previous one.
7. The `pendingPoints` will be calculated as `1e18 * 25e36 / 1e18 = 25e36`, which is correct.

## Recommendation

Ensure that the protocol correctly follows the precision multiplier of `1e18` in all calculations.

<details>

<summary>Diff</summary>

```diff
@@ -358,7 +358,7 @@ contract SophonFarming is Upgradeable2Step, SophonFarmingState {
         PoolInfo storage pool = poolInfo[_pid];
         UserInfo storage user = userInfo[_pid][_user];
 
-        uint256 accPointsPerShare = pool.accPointsPerShare * 1e18;
+        uint256 accPointsPerShare = pool.accPointsPerShare;
 
         uint256 lpSupply = pool.amount;
         if (getBlockNumber() > pool.lastRewardBlock && lpSupply != 0) {
@@ -378,7 +378,7 @@ contract SophonFarming is Upgradeable2Step, SophonFarmingState {
 
         return user.amount *
             accPointsPerShare /
-            1e36 +
+            1e18 +
             user.rewardSettled -
             user.rewardDebt;
     }
@@ -427,7 +427,7 @@ contract SophonFarming is Upgradeable2Step, SophonFarmingState {
             _allocPoint /
             totalAllocPoint;
 
-        pool.accPointsPerShare = pointReward /
+        pool.accPointsPerShare = pointReward * 1e18 /
             lpSupply +
             pool.accPointsPerShare;
```

</details>