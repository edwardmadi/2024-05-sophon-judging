Striped Orchid Donkey

medium

# Incorrect accounting of reward points can be caused by owner changing the startBlock

## Summary
If owner changes the `startBlock`, the reward points can be incorrectly calculated, because `pool.lastRewardBlock` is not updated accordingly.

## Vulnerability Detail
When a pool is added, `pool.lastRewardBlock` is initialized to [startBlock](https://github.com/sherlock-audit/2024-05-sophon/blob/main/farming-contracts/contracts/farm/SophonFarmingState.sol#L79) which indicates the block number when point mining starts.
```solidity
    function add(uint256 _allocPoint, address _lpToken, string memory _description, bool _withUpdate) public onlyOwner returns (uint256) {
        ...

@=>     uint256 lastRewardBlock = getBlockNumber() > startBlock ? getBlockNumber() : startBlock;
        
        ...

        poolInfo.push(
            PoolInfo({
                lpToken: IERC20(_lpToken),
                l2Farm: address(0),
                amount: 0,
                boostAmount: 0,
                depositAmount: 0,
                allocPoint: _allocPoint,
@=>             lastRewardBlock: lastRewardBlock,
                accPointsPerShare: 0,
                description: _description
            })
        );

        ...
    }
```
If a user deposits before `startBlock`, no reward points will be accumulated until `startBlock`. This is because when pool won't be  updated if the current block number is less than `pool.lastRewardBlock`, hence `pool.accPointsPerShare` remains 0.
```solidity
    function updatePool(uint256 _pid) public {
        PoolInfo storage pool = poolInfo[_pid];
@=>     if (getBlockNumber() <= pool.lastRewardBlock) {
@=>         return;
@=>     }

        ...
    }
```
At the same time, `startBlock` can be changed by the owner if the current block number is less than or equal to `startBlock`.
```solidity
    function setStartBlock(uint256 _startBlock) public onlyOwner {
        if (_startBlock == 0 || (endBlock != 0 && _startBlock >= endBlock)) {
            revert InvalidStartBlock();
        }
        if (getBlockNumber() > startBlock) {
            revert FarmingIsStarted();
        }
        startBlock = _startBlock;
```
However, the `pool.lastRewardBlock` of each pool is not updated accordingly, and this may lead to incorrect accounting of reward points. Consider the following case:
1. Before `startBlock`, owner calls to change it to a later value;
2. Bob frontruns to deposit 1 ether ETH;
3. At the block of `startBlock` (new value), Alice deposits 1 ether ETH, both Alice and Bob starts to earn reward points;
4. It is expected that Alice and Bob will earn the same rewards points during the staking period, however, because `pool.lastRewardBlock` is not updated, its value is less than `startBlock`, this means extra blocks (`startBlock - pool.lastRewardBlock`) is included in calculation of reward points, results in Bob earns more reward points than Alice.

Similarly, if owner changes `startBlock` to an earlier value, users will earn less reward points than expected, as less blocks (` pool.lastRewardBlock - startBlock`) is included.

Please see the below test codes to verify:
```solidity
contract SophonFarmingForkTest is Test {
    string MAINNET_RPC_URL = vm.envString("MAINNET_RPC_URL");

    address deployer = makeAddr("deployer");
    address alice = makeAddr("Alice");
    address bob = makeAddr("Bob");

    IWeth weth = IWeth(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);
    IstETH stETH = IstETH(0xae7ab96520DE3A18E5e111B5EaAb095312D7fE84);
    IwstETH wstETH = IwstETH(0x7f39C581F595B53c5cb19bD0b3f8dA6c935E2Ca0);
    IweETH weETH = IweETH(0xCd5fE23C85820F7B72D0926FC9b05b43E359b7ee);

    IERC20 eETH = IERC20(0x35fA164735182de50811E8e2E824cFb9B6118ac2);
    IeETHLiquidityPool eETHLiquidityPool = IeETHLiquidityPool(0x308861A430be4cce5502d0A12724771Fc6DaF216);
    
    IERC20 dai = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
    IsDAI sDAI = IsDAI(0x83F20F44975D03b1b09e64809B757c47f942BEeA);

    uint256 internal wstETHAllocPoint;
    uint256 internal sDAIAllocPoint;
    uint256 internal pointsPerBlock;
    uint256 internal startBlock;
    uint256 internal boosterMultiplier;
    uint256 maxUint = type(uint256).max;

    SophonFarmingProxy public sophonFarmingProxy;
    SophonFarming public sophonFarming;
    address public implementation;

    function setUp() public {
        vm.createSelectFork(MAINNET_RPC_URL);

        // Deal and start prank
        vm.deal(deployer, 1000000e18);
        vm.startPrank(deployer);

        wstETHAllocPoint = 20000;
        sDAIAllocPoint = 20000;

        pointsPerBlock = 25e18;
        startBlock = block.number;
        boosterMultiplier = 2e18;

        // Deploy implementation
        implementation = address(
            new SophonFarming(
                [
                    address(dai),
                    address(sDAI),
                    address(weth),
                    address(stETH),
                    address(wstETH),
                    address(eETH),
                    address(eETHLiquidityPool),
                    address(weETH)
                ]
            )
        );

        // Deploy proxy
        sophonFarmingProxy = new SophonFarmingProxy(implementation);
        (bool success, ) = address(sophonFarmingProxy).call(abi.encodeWithSignature(
            "initialize(uint256,uint256,uint256,uint256,uint256)", 
            wstETHAllocPoint, sDAIAllocPoint, pointsPerBlock, startBlock, boosterMultiplier
        ));
        require(success, "Fail to call");
        (success, ) = address(sophonFarmingProxy).call(abi.encodeWithSignature("setEndBlock(uint256,uint256)", maxUint - 1000, 1000));
        require(success, "Fail to call");

        // Initialize SophonFarming
        sophonFarming = SophonFarming(payable(address(implementation)));
        sophonFarming.initialize(wstETHAllocPoint, sDAIAllocPoint, pointsPerBlock, startBlock, boosterMultiplier);
        sophonFarming.setEndBlock(maxUint - 1000, 1000);

        vm.stopPrank();

        // Label
        vm.label(deployer, "Deployer");
        vm.label(address(weth), "WETH");
        vm.label(address(stETH), "stETH");
        vm.label(address(wstETH), "wstETH");
        vm.label(address(weETH), "weETH");
        vm.label(address(eETH), "eETH");
        vm.label(address(eETHLiquidityPool), "eETH Liquidity Pool");
        vm.label(address(dai), "DAI");
        vm.label(address(sDAI), "sDAI");
    }

    function test_audit_proxy_set_start_block() public {
        (bool success, bytes memory result) = address(sophonFarmingProxy).call(
            abi.encodeWithSignature("startBlock()")
        );
        require(success, "fail to call");
        uint256 startBlock = abi.decode(result, (uint256));

        // Bob frontruns to deposit
        deal(bob, 1 ether);
        vm.prank(bob);
        (success, ) = address(sophonFarmingProxy).call{value: 1 ether}("");
        require(success, "fail to call");

        // Owner sets a later start block
        vm.prank(deployer);
        (success, ) = address(sophonFarmingProxy).call(abi.encodeWithSignature("setStartBlock(uint256)", startBlock + 10));
        require(success, "fail to call");
        startBlock = startBlock + 10;

        // Jump to start block
        vm.roll(startBlock);

        // Alice deposit
        deal(alice, 1 ether);
        vm.prank(alice);
        (success, ) = address(sophonFarmingProxy).call{value: 1 ether}("");
        require(success, "fail to call");

        // Jump to 10 blocks in the future
        vm.roll(block.number + 10);

        // Get pool pid (wstETH)
        (success, result) = address(sophonFarmingProxy).call(abi.encodeWithSignature("typeToId(uint8)", 1));
        require(success, "fail to call");
        uint256 pid = abi.decode(result, (uint256));

        // Get Alice's pending reward points
        (success, result) = address(sophonFarmingProxy).call(abi.encodeWithSignature("pendingPoints(uint256,address)", pid, alice));
        require(success, "fail to call");
        uint256 alicePendingRewardPoints = abi.decode(result, (uint256));

        // Get Bob's pending reward points
        (success, result) = address(sophonFarmingProxy).call(abi.encodeWithSignature("pendingPoints(uint256,address)", pid, bob));
        require(success, "fail to call");
        uint256 bobPendingRewardPoints = abi.decode(result, (uint256));
        
        // Alice earns less reward points than Bob
        assertTrue(alicePendingRewardPoints < bobPendingRewardPoints);
    }
}

```

## Impact
Incorrect accounting of reward points.

## Code Snippet
https://github.com/sherlock-audit/2024-05-sophon/blob/main/farming-contracts/contracts/farm/SophonFarming.sol#L272-L280

## Tool used
Manual Review

## Recommendation
Update the `pool.lastRewardBlock` of each pool when updating `startBlock`.
```diff
    function setStartBlock(uint256 _startBlock) public onlyOwner {
        if (_startBlock == 0 || (endBlock != 0 && _startBlock >= endBlock)) {
            revert InvalidStartBlock();
        }
        if (getBlockNumber() > startBlock) {
            revert FarmingIsStarted();
        }
        startBlock = _startBlock;

+        uint256 length = poolInfo.length;
+        for(uint256 pid = 0; pid < length;) {
+            PoolInfo storage pool = poolInfo[pid];
+            pool.lastRewardBlock = startBlock;
+            unchecked { ++pid; }
+        }
    }
```
