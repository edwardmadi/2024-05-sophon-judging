Striped Orchid Donkey

medium

# Setting alloc point without updating pool may lead to user earns less / more reward points than expected

## Summary
If there are deposits in the pool, setting alloc point without updating pool may lead to user earns less / more reward points than expected. There may happen even when owner submit before farming starts.

## Vulnerability Detail
When owner calls function [set()](https://github.com/sherlock-audit/2024-05-sophon/blob/main/farming-contracts/contracts/farm/SophonFarming.sol#L195) to update a given pool's allocation point, they may set `_withUpdate` argument to `false` and if so, no pool will be updated.
```solidity
    function set(uint256 _pid, uint256 _allocPoint, bool _withUpdate) public onlyOwner {
        ...

        if (_withUpdate) {
            massUpdatePools();
        }

        ...
    }
```
It is expected that the updated `allocPoint` should only be applied to the future blocks, user's reward points earned in the blocks prior to the current block is not affected. However, since the pool is not updated, some older blocks may accrue more / less reward points per share.

Precisely, let's say the pool is updated at **blockA**, current block is **block B**, if `allocPoint` is updated to a higher value, then each block between **[blockA, blockB]** accrues more reward points per share than expected, and if `allocPoint` is updated to a lower value, then each block accrues less points per share, as a result, user earns less / more reward points than expected.

Please note this is not an admin error, owner may choose to update `allocPoint` before `startBlock` before which no rewards accrues, however, the TX might be pending for a long time and when it is executed, `startBlock` has been passed and there are already deposits in the pool.

The output of the test codes below shows the difference reward points earned when owner decrease `allocPoint` with / without updating the pool:
> Case 1: 66666666666666666665
> Case 2: 49999999999999999999

```solidity
contract SophonFarmingForkTest is Test {
    string MAINNET_RPC_URL = vm.envString("MAINNET_RPC_URL");

    address deployer = makeAddr("deployer");
    address alice = makeAddr("Alice");
    address bob = makeAddr("Bob");

    IWeth weth = IWeth(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);
    IstETH stETH = IstETH(0xae7ab96520DE3A18E5e111B5EaAb095312D7fE84);
    IwstETH wstETH = IwstETH(0x7f39C581F595B53c5cb19bD0b3f8dA6c935E2Ca0);
    IweETH weETH = IweETH(0xCd5fE23C85820F7B72D0926FC9b05b43E359b7ee);

    IERC20 eETH = IERC20(0x35fA164735182de50811E8e2E824cFb9B6118ac2);
    IeETHLiquidityPool eETHLiquidityPool = IeETHLiquidityPool(0x308861A430be4cce5502d0A12724771Fc6DaF216);
    
    IERC20 dai = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
    IsDAI sDAI = IsDAI(0x83F20F44975D03b1b09e64809B757c47f942BEeA);

    uint256 internal wstETHAllocPoint;
    uint256 internal sDAIAllocPoint;
    uint256 internal pointsPerBlock;
    uint256 internal startBlock;
    uint256 internal boosterMultiplier;
    uint256 maxUint = type(uint256).max;

    SophonFarmingProxy public sophonFarmingProxy;
    SophonFarming public sophonFarming;
    address public implementation;

    function setUp() public {
        vm.createSelectFork(MAINNET_RPC_URL, 19923625);

        // Deal and start prank
        vm.deal(deployer, 1000000e18);
        vm.startPrank(deployer);

        wstETHAllocPoint = 20000;
        sDAIAllocPoint = 20000;

        pointsPerBlock = 25e18;
        startBlock = block.number;
        boosterMultiplier = 2e18;

        // Deploy implementation
        implementation = address(
            new SophonFarming(
                [
                    address(dai),
                    address(sDAI),
                    address(weth),
                    address(stETH),
                    address(wstETH),
                    address(eETH),
                    address(eETHLiquidityPool),
                    address(weETH)
                ]
            )
        );

        // Deploy proxy
        sophonFarmingProxy = new SophonFarmingProxy(implementation);
        (bool success, ) = address(sophonFarmingProxy).call(abi.encodeWithSignature(
            "initialize(uint256,uint256,uint256,uint256,uint256)", 
            wstETHAllocPoint, sDAIAllocPoint, pointsPerBlock, startBlock, boosterMultiplier
        ));
        require(success, "Fail to call");
        (success, ) = address(sophonFarmingProxy).call(abi.encodeWithSignature("setEndBlock(uint256,uint256)", maxUint - 1000, 1000));
        require(success, "Fail to call");

        // Initialize SophonFarming
        sophonFarming = SophonFarming(payable(address(implementation)));
        sophonFarming.initialize(wstETHAllocPoint, sDAIAllocPoint, pointsPerBlock, startBlock, boosterMultiplier);
        sophonFarming.setEndBlock(maxUint - 1000, 1000);

        vm.stopPrank();

        // Label
        vm.label(deployer, "Deployer");
        vm.label(address(weth), "WETH");
        vm.label(address(stETH), "stETH");
        vm.label(address(wstETH), "wstETH");
        vm.label(address(weETH), "weETH");
        vm.label(address(eETH), "eETH");
        vm.label(address(eETHLiquidityPool), "eETH Liquidity Pool");
        vm.label(address(dai), "DAI");
        vm.label(address(sDAI), "sDAI");
    }

    function test_audit_proxy_set_allocPoint_without_update() public {
        // Jump to start block
        (bool success, bytes memory result) = address(sophonFarmingProxy).call(
            abi.encodeWithSignature("startBlock()")
        );
        require(success, "fail to call");
        uint256 startBlock = abi.decode(result, (uint256));
        vm.roll(startBlock);

        deal(alice, 1 ether);
        vm.prank(alice);
        (success, ) = address(sophonFarmingProxy).call{value: 1 ether}("");
        require(success, "fail to call");

        // Get wstETH pool pid
        (success, result) = address(sophonFarmingProxy).call(abi.encodeWithSignature("typeToId(uint8)", 1));
        require(success, "fail to call");
        uint256 pid = abi.decode(result, (uint256));

        // Get Alice's deposit amount
        (success, result) = address(sophonFarmingProxy).call(abi.encodeWithSignature("userInfo(uint256,address)", pid, alice));
        require(success, "fail to call");
        (,, uint256 aliceDepositAmount,,) = abi.decode(result, (uint256,uint256,uint256,uint256,uint256));

        /** 
            Case 1: alloc point is NOT changed
        */

        // Jump to 5 block in the futrure
        vm.roll(block.number + 5);

        // Change alloc point to half
        (success, result) = address(sophonFarmingProxy).call(abi.encodeWithSignature("poolInfo(uint256)", pid));
        require(success, "fail to call");
        (,,,,, uint256 allocPoint,,,) = abi.decode(result, (address, address, uint256, uint256, uint256, uint256, uint256, uint256, string));

        vm.prank(deployer);
        (success, ) = address(sophonFarmingProxy).call(abi.encodeWithSignature("set(uint256,uint256,bool)", pid, allocPoint / 2, true));
        require(success, "fail to call");

        // Jump to 5 block again in the futrure
        vm.roll(block.number + 5);

        // Update pool
        (success, ) = address(sophonFarmingProxy).call(abi.encodeWithSignature("updatePool(uint256)", pid));
        require(success, "fail to call");

        // Get Alice's reward points
        (success, result) = address(sophonFarmingProxy).call(abi.encodeWithSignature("pendingPoints(uint256,address)", pid, alice));
        require(success, "fail to call");
        uint256 pendingPoints = abi.decode(result, (uint256));
        console.log("Case 1:", pendingPoints);

        /** 
            Case 2: alloc point IS changed
        */

        // // Jump to 5 block in the futrure
        // vm.roll(block.number + 5);

        // // Change alloc point to half
        // (success, result) = address(sophonFarmingProxy).call(abi.encodeWithSignature("poolInfo(uint256)", pid));
        // require(success, "fail to call");
        // (,,,,, uint256 allocPoint,,,) = abi.decode(result, (address, address, uint256, uint256, uint256, uint256, uint256, uint256, string));

        // vm.prank(deployer);
        // (success, ) = address(sophonFarmingProxy).call(abi.encodeWithSignature("set(uint256,uint256,bool)", pid, allocPoint / 2, false));
        // require(success, "fail to call");

        // // Jump to 5 block again in the futrure
        // vm.roll(block.number + 5);

        // // Update pool
        // (success, ) = address(sophonFarmingProxy).call(abi.encodeWithSignature("updatePool(uint256)", pid));
        // require(success, "fail to call");

        //  // Get Alice's reward points
        // (success, result) = address(sophonFarmingProxy).call(abi.encodeWithSignature("pendingPoints(uint256,address)", pid, alice));
        // require(success, "fail to call");
        // uint256 pendingPoints = abi.decode(result, (uint256));
        // console.log("Case 2:", pendingPoints);
    }
}

```

## Impact
User earns less / more reward points than expected.

## Code Snippet
https://github.com/sherlock-audit/2024-05-sophon/blob/main/farming-contracts/contracts/farm/SophonFarming.sol#L199-L201

## Tool used
Manual Review

## Recommendation
Should always update pool when farming has started and there are deposits in the pool.
```diff
    function set(uint256 _pid, uint256 _allocPoint, bool _withUpdate) public onlyOwner {
       ...

-      if (_withUpdate) {
+      if (_withUpdate || (getBlockNumber() >= startBlock && pool.amount > 0)) {
            massUpdatePools();
       }

        ...
    }
```