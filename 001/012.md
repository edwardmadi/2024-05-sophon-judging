Proper Admiral Dalmatian

medium

# Conditional use of `_withUpdate` in `SophonFarming::set()` and `SophonFarming::add()` could lead to improper reward calculation

## Summary

In the `SophonFarming` contract, the `totalAllocPoint` variable is used to correctly determine what will be the portion that each pool would get from the total reward, making it detrimental to the reward calculation. This means that, whenever the `totalAllocPoint` is updated without updating the pending reward first, the reward calculation will be incorrect.

## Vulnerability Detail

https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L153
https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L195

In the `add()` and `set()` functions shown above, if `_withUpdate` is set to false, the `totalAllocPoint` variable will be modified without updating the rewards (`massUpdatePools()`).

## Impact

This vulnerability could lead to improper reward calculation, resulting in a loss of funds for the users.

## Code Snippet

```solidity
    function add(uint256 _allocPoint, address _lpToken, string memory _description, bool _withUpdate) public onlyOwner returns (uint256) {
        if (poolExists[_lpToken]) {
            revert PoolExists();
        }
        if (isFarmingEnded()) {
            revert FarmingIsEnded();
        }
@>        if (_withUpdate) { // conditional logic
            massUpdatePools();
        }
        uint256 lastRewardBlock =
            getBlockNumber() > startBlock ? getBlockNumber() : startBlock;
@>        totalAllocPoint = totalAllocPoint + _allocPoint; // update totalAllocPoint without updating rewards if the conditional is false
        poolExists[_lpToken] = true;
...
    }
```

```solidity
    function set(uint256 _pid, uint256 _allocPoint, bool _withUpdate) public onlyOwner {
        if (isFarmingEnded()) {
            revert FarmingIsEnded();
        }
@>        if (_withUpdate) { // conditional logic
            massUpdatePools();
        }

        PoolInfo storage pool = poolInfo[_pid];
        address lpToken = address(pool.lpToken);
        if (lpToken == address(0) || !poolExists[lpToken]) {
            revert PoolDoesNotExist();
        }
@>        totalAllocPoint = totalAllocPoint - pool.allocPoint + _allocPoint; // update totalAllocPoint without updating rewards if the conditional is false
        pool.allocPoint = _allocPoint;
...
    }
```

## Tool used

Manual Review

## Proof of concept

1. Let's assume that on block `1000000`, we have `pointPerBlock = 5`, `totalAllocPoint = 5000`, and `pool.allocPoint = 500` and all rewards are updated.
2. The owner uses `add()` on block `1100000`, increasing the `totalAllocPoint` to `10000` without updating the pool rewards (`_withUpdate = false`).
3. All pools are again updated on block `1200000`.

From the above scenario, the `pointReward` from block `1000000` to `1200000` will be:

```solidity
pointRewardAt1200000 = blockMultiplier * pointsPerBlock * pool.allocPoint / totalAllocPoint;

pointRewardAt1200000 = 200000 * 5 * 500 / 10000 = 50000
```

However, the rewards should be calculated by accounting for the original `totalAllocPoint` value during the period when it is not yet updated as follows:

```solidity
pointRewardAt1100000 = blockMultiplier * pointsPerBlock * pool.allocPoint / totalAllocPoint

pointRewardAt1100000 = 100000 * 5 * 500 / 5000 = 50000 
```

Then at `110000`, the rewards are properly updates, so at `1200000`:

```solidity
pointRewardFrom110000to120000 = blockMultiplier * pointsPerBlock * pool.allocPoint / totalAllocPoint

pointRewardFrom110000to120000 = 100000 * 5 * 500 / 10000 = 25000
```

We can now see that the reward should be `50000 + 25000 = 75000`, differing from the one calculated without updating the rewards with `25000`.

## Recommendation

Remove the conditional logic (`bool _withUpdate`) and always update the rewards, before updating `totalAllocPoint` in the `add()` and `set()` functions.

<details>

<summary>Diff</summary>

```diff
@@ -147,19 +147,17 @@ contract SophonFarming is Upgradeable2Step, SophonFarmingState {
      * @param _allocPoint alloc point for new pool
      * @param _lpToken lpToken address
      * @param _description description of new pool
-     * @param _withUpdate True will update accounting for all pools
      * @return uint256 The pid of the newly created asset
+     * // audit - missing zero address check for _lpToken
      */
-    function add(uint256 _allocPoint, address _lpToken, string memory _description, bool _withUpdate) public onlyOwner returns (uint256) {
+    function add(uint256 _allocPoint, address _lpToken, string memory _description) public onlyOwner returns (uint256) {
         if (poolExists[_lpToken]) {
             revert PoolExists();
         }
         if (isFarmingEnded()) {
             revert FarmingIsEnded();
         }
-        if (_withUpdate) {
-            massUpdatePools();
-        }
+        massUpdatePools();
         uint256 lastRewardBlock =
             getBlockNumber() > startBlock ? getBlockNumber() : startBlock;
         totalAllocPoint = totalAllocPoint + _allocPoint;
@@ -190,15 +188,12 @@ contract SophonFarming is Upgradeable2Step, SophonFarmingState {
      * @notice Updates the given pool's allocation point. Can only be called by the owner.
      * @param _pid The pid to update
      * @param _allocPoint The new alloc point to set for the pool
-     * @param _withUpdate True will update accounting for all pools
      */
-    function set(uint256 _pid, uint256 _allocPoint, bool _withUpdate) public onlyOwner {
+    function set(uint256 _pid, uint256 _allocPoint) public onlyOwner {
         if (isFarmingEnded()) {
             revert FarmingIsEnded();
         }
-        if (_withUpdate) {
-            massUpdatePools();
-        }
+        massUpdatePools();
 
         PoolInfo storage pool = poolInfo[_pid];
         address lpToken = address(pool.lpToken);
```

</details>