Droll Olive Rook

medium

# Missing pool type check on `SophosFarming::depositEth` and `SophosFarming::depositWeth` could lead a user to loose `ETH` funds forever

## Summary

Missing pool type check on `SophosFarming::depositEth` and `SophosFarming::depositWeth` could lead a user to deposit `ETH` to the farming contract but his points and rewards are counted for deploying `DAI`, while having no possibility to withdraw the deposited `ETH` amount.

## Vulnerability Detail

The functions `depositEth` and `depositWeth` allow a user to deposit `ETH` and `WETH` respectively.
However, the `_predefinedPool` is provided by the end user, as described below.

```javacscript

@>    function depositEth(uint256 _boostAmount, PredefinedPool _predefinedPool) public payable {
        if (msg.value == 0) {
            revert NoEthSent();
        }

        uint256 _finalAmount = msg.value;
        if (_predefinedPool == PredefinedPool.wstETH) {
            _finalAmount = _ethTOstEth(_finalAmount);
        } else if (_predefinedPool == PredefinedPool.weETH) {
            _finalAmount = _ethTOeEth(_finalAmount); // e converting eth to eEth
        }

        _depositPredefinedAsset(_finalAmount, msg.value, _boostAmount, _predefinedPool);
    }

```

if the `_predefinedPool` parameter has the value `PredefinedPool.sDAI` instead of `PredefinedPool.wstETH` or `PredefinedPool.weETH`, the user actually deposits `ETH` to the `SophonFarming` contract but her/his deposit records, e.g. points and rewards, are updated on the `sDAI` pool. In addition, there will be no record of deploying `ETH` for her/him after such operation, hence, there will be no possible withdrawal of the deposited `ETH` amount.

Links to the affected code:
- `depositEth` function: https://github.com/sherlock-audit/2024-05-sophon/blob/main/farming-contracts/contracts/farm/SophonFarming.sol#L503
- `depositWeth` function: https://github.com/sherlock-audit/2024-05-sophon/blob/main/farming-contracts/contracts/farm/SophonFarming.sol#L524

## Impact

A user could deposit `ETH` but his points and rewards are wrongly calculated for the `sDAI` pool. As a consequence, he could never withdraw the deposited `ETH` from the protocol.

## Code Snippet
Scenario:
1. An attacker donates an amount of DAI to the SophonFarming contract beforehand (or front-run the user transaction in step 2)
2. A user deposits `ETH` to the `sDAI` pool by mistake using the function `depositEth`
3. He tries to withdraw the deposited `ETH` without success.

The following proof of code can be appended to the file `SophonFarming.t.sol`. Run the test with the command: `forge test --mt test_DepositEth_NotBoosted_To_sDAI_Pool_Cant_Withdraw_ETH_Afterward`.

<details>
<summary>Code</summary>

```javascript

function test_DepositEth_NotBoosted_To_sDAI_Pool_Cant_Withdraw_ETH_Afterward() public {
        address attacker = makeAddr("attacler");
        uint256 amountDaiToDeposit = 100e18;

        deal(address(dai), attacker, amountDaiToDeposit); // the attacker has 100 DAI

        // 1. The attacker donates 100 DAIs to the sophon farming contract
        vm.startPrank(attacker);
        dai.transferFrom(attacker, address(sophonFarming), amountDaiToDeposit);
        vm.stopPrank();

        // 2. account1 want to deposit 100 ETH to the farming contract
        // the attacker just needs amountDaiToDeposit to be bigger or equal than amountETHToDeposit to reproduce the issue
        uint256 amountETHToDeposit = 100e18;
        vm.deal(account1, amountETHToDeposit);

        vm.startPrank(account1);
        uint256 wsthDepositedAmount = WstETHRate(StETHRate(amountETHToDeposit));
        // NOTE: account1 deposits ETH but puts the wrong pool type
        sophonFarming.depositEth{value: amountETHToDeposit}(0, SophonFarmingState.PredefinedPool.sDAI);
        assertEq(address(account1).balance, 0); // account1's ETH balance is now zero
        vm.stopPrank();

        // 3. acccount1 wants to withdraw his 100 ETH deposited but he can't
        uint256 wstETHPoolId = sophonFarming.typeToId(SophonFarmingState.PredefinedPool.wstETH);
        SophonFarmingState.UserInfo memory userInfo = getUserInfo(wstETHPoolId, account1);

        vm.startPrank(account1);
        vm.expectRevert(abi.encodeWithSelector(SophonFarming.WithdrawTooHigh.selector, userInfo.depositAmount));
        // The withdraw reverts as the current ETH deposited by account1 in the protocol's point of view is 0,
        // instead of 100 ETH as the user expected
        sophonFarming.withdraw(wstETHPoolId, amountETHToDeposit);
        assertFalse(userInfo.depositAmount == amountDaiToDeposit);
        assertEq(userInfo.depositAmount, 0);

        // He now have a positive amount of sDAI recorded in the sDai pool
        // despite the fact that he never deposit DAI to the SophonFarming contract
        uint256 sDaiPoolId = sophonFarming.typeToId(SophonFarmingState.PredefinedPool.sDAI);
        SophonFarmingState.UserInfo memory userInfoInsDaiPool = getUserInfo(sDaiPoolId, account1);
        assertTrue(userInfoInsDaiPool.depositAmount > 0);

        vm.stopPrank();
}

```
</details>

Note that the same issue can happen for a user depositing `WETH` to the `sDAI` pool by mistake using the function `depositWeth`. The deposited `WETH` fund will get locked in the `SophonFarming` contract and he won't be able to retrieve it.

## Tool used

Manual Review

## Recommendation

It's recommended to add a safeguard in both functions `depositEth` and `depositWeth` to prevent a user from depositing `ETH` or `WETH` into the `sDAI` pool.
Consider adding the following changes to the file `SophonFarming.sol`:

```diff

function depositEth(uint256 _boostAmount, PredefinedPool _predefinedPool) public payable {
...
        if (_predefinedPool == PredefinedPool.wstETH) {
            _finalAmount = _ethTOstEth(_finalAmount); // e converting eth to stEth
        } else if (_predefinedPool == PredefinedPool.weETH) {
            _finalAmount = _ethTOeEth(_finalAmount); // e converting eth to eEth
+       } else {
+           revert InvalidDeposit();
+       }

...
}

function depositWeth(uint256 _amount, uint256 _boostAmount, PredefinedPool _predefinedPool) external {
...
        if (_predefinedPool == PredefinedPool.wstETH) {
            _finalAmount = _ethTOstEth(_finalAmount);
        } else if (_predefinedPool == PredefinedPool.weETH) {
            _finalAmount = _ethTOeEth(_finalAmount);
+       } else {
+           revert InvalidDeposit();
+       }
...
}

```

The two test cases below can also be appended to the file `SophonFarming.t.sol` to prevent future regression.

<details>
<summary>Code</summary>

```javascript

function test_DepositEth_To_sDAI_Pool_Revert() public {
        uint256 amountETHToDeposit = 100e18;
        vm.deal(account1, amountETHToDeposit);

        vm.startPrank(account1);
        uint256 wsthDepositedAmount = WstETHRate(StETHRate(amountETHToDeposit));
        vm.expectRevert(SophonFarming.InvalidDeposit.selector);
        sophonFarming.depositEth{value: amountETHToDeposit}(0, SophonFarmingState.PredefinedPool.sDAI);
        vm.stopPrank();
    }

    function test_DepositWeth_To_sDAI_Pool_Revert() public {
        uint256 amountWETHToDeposit = 100e18;

        vm.startPrank(account1);
        vm.deal(account1, amountWETHToDeposit);
        weth.deposit{value: amountWETHToDeposit}();
        assertEq(weth.balanceOf(account1), amountWETHToDeposit);

        weth.approve(address(sophonFarming), amountWETHToDeposit);
        vm.expectRevert(SophonFarming.InvalidDeposit.selector);
        sophonFarming.depositWeth(amountWETHToDeposit, 0, SophonFarmingState.PredefinedPool.sDAI);
        vm.stopPrank();
    }


```

</details>