Attractive Grey Cow

medium

# Ended farming can be restarted unintentionally leading to incorrect points/rewards distributions and deposited tokens can be mistakenly withdrawn

## Summary

The `SophonFarming::setEndBlock()` does not check whether farming is ended while resetting the `endBlock` state variable to 0. Subsequently, the ended farming can be switched on unintentionally, breaking the protocol's core invariant.

## Vulnerability Detail

The `setEndBlock()` allows an owner to set the `endBlock` state variable. The `endBlock` will affect all farming pools. In case the `endBlock` is a positive number, it represents the block number for ending all farming pools (the `if` case below). Whereas the `endBlock` == 0 represents the state of no ending block decided yet (the `else` case below). 

The `setEndBlock()` verifies that [farming has not ended](https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L293-L295) when setting the `endBlock` state variable with a positive number (the `if` case). However, the function [lacks checking whether farming has ended](https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L297-L300) while setting the `endBlock` state variable to 0 (the `else` case).

Hence, the `setEndBlock()` can mistakenly allow the ended farming to restart.

```solidity
    function setEndBlock(uint256 _endBlock, uint256 _withdrawalBlocks) public onlyOwner {
        uint256 _endBlockForWithdrawals;
        if (_endBlock != 0) {
            if (_endBlock <= startBlock || getBlockNumber() > _endBlock) {
                revert InvalidEndBlock();
            }
            if (isFarmingEnded()) {
                revert FarmingIsEnded();
            }
            _endBlockForWithdrawals = _endBlock + _withdrawalBlocks;

        //@audit -- The setEndBlock() lacks checking whether farming is ended while resetting the endBlock state variable to 0
@1      } else {
@1          // withdrawal blocks needs an endBlock
@1          _endBlockForWithdrawals = 0;
@1      }
        massUpdatePools();
        endBlock = _endBlock;
        endBlockForWithdrawals = _endBlockForWithdrawals;
    }
```
- `@1 -- The setEndBlock() lacks checking whether farming is ended while resetting the endBlock state variable to 0`: https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L297-L300

After resetting the `endBlock` state variable to 0, the [ended farming will be restarted](https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L235) unintentionally. 

Further, when the `endBlock` is reset to 0 in the `setEndBlock()`, the [`endBlockForWithdrawals` state variable is also reset to 0](https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L299). This unintentionally [allows deposited tokens to be withdrawn](https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L248).

```solidity
    function isFarmingEnded() public view returns (bool) {
        uint256 _endBlock = endBlock;
        if (_endBlock != 0 && getBlockNumber() > _endBlock) {
            return true;
        } else {
@2.1        return false; //@audit -- After the endBlock state variable is reset to 0, the ended farming will be switched on unintentionally
        }
    }

    ...

    function isWithdrawPeriodEnded() public view returns (bool) {
        uint256 _endBlockForWithdrawals = endBlockForWithdrawals;
        if (_endBlockForWithdrawals != 0 && getBlockNumber() > _endBlockForWithdrawals) {
            return true;
        } else {
@2.2        return false; //@audit -- The endBlockForWithdrawals == 0 (being reset in @1) also unintentionally allows deposited tokens to be withdrawn
        }
    }
```
- `@2.1 -- After the endBlock state variable is reset to 0, the ended farming will be switched on unintentionally`: https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L235
- `@2.2 -- The endBlockForWithdrawals == 0 (being reset in @1) also unintentionally allows deposited tokens to be withdrawn`: https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L248

After the ended farming has restarted, the `SophonFarming::updatePool()` continues to [distribute points/rewards to users staking in pools by mistake](https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L423). The root cause is that since the `endBlock` == 0, the [check for the end of farming](https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L341-L343) in the `SophonFarming::_getBlockMultiplier()` will be bypassed.

```solidity
    function updatePool(uint256 _pid) public {
        ...

        //@audit -- After restarting the ended farming, the updatePool() continues to distribute points/rewards to users staking in pools by mistake
@3.1    uint256 blockMultiplier = _getBlockMultiplier(pool.lastRewardBlock, getBlockNumber());
        uint256 pointReward =
            blockMultiplier *
            _pointsPerBlock *
            _allocPoint /
            totalAllocPoint;

        pool.accPointsPerShare = pointReward /
            lpSupply +
            pool.accPointsPerShare;

        pool.lastRewardBlock = getBlockNumber();
    }

    ...

    function _getBlockMultiplier(uint256 _from, uint256 _to) internal view returns (uint256) {
        uint256 _endBlock = endBlock;

        //@audit -- Since the endBlock == 0, the check for the end of farming is bypassed
@3.2    if (_endBlock != 0) {
@3.2        _to = Math.min(_to, _endBlock);
@3.2    }

        if (_to > _from) {
            return (_to - _from) * 1e18;
        } else {
            return 0;
        }
    }
```
- `@3.1 -- After restarting the ended farming, the updatePool() continues to distribute points/rewards to users staking in pools by mistake`: https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L423
- `@3.2 -- Since the endBlock == 0, the check for the end of farming is bypassed`: https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L341-L343

## Coded PoC

The coded PoC is presented below. 

Please place the code in the test file: `farming-contracts/test/SophonFarming.t.sol`. To run the code, execute the command: `forge test -vvv --match-test test_PocRestartEndedFarming`.

The PoC proves that the `setEndBlock()` can mistakenly allow the ended farming to restart. As a result, the deposited tokens are unintentionally allowed to be withdrawn. Moreover, the restarted farming also continues to distribute points/rewards to users staking in pools incorrectly.

```solidity
function test_PocRestartEndedFarming() public {
    // Deposit 1 DAI for PoC demonstration
    uint256 amountToDeposit = 1e18;
    deal(address(dai), account1, amountToDeposit);
    assertEq(dai.balanceOf(account1), amountToDeposit);

    vm.startPrank(account1);
    dai.approve(address(sophonFarming), amountToDeposit);
    sophonFarming.depositDai(amountToDeposit, 0);
    assertEq(dai.balanceOf(account1), 0);
    vm.stopPrank();

    vm.startPrank(deployer);

    // Farming is not ended yet
    assertEq(sophonFarming.isFarmingEnded(), false);
    assertEq(sophonFarming.isWithdrawPeriodEnded(), false);

    // Set the endBlock == block.number + 10 to end farming after the next 10 block
    sophonFarming.setEndBlock(block.number + 10, 1);
    assertEq(sophonFarming.isFarmingEnded(), false);
    assertEq(sophonFarming.isWithdrawPeriodEnded(), false);

    // Farming was already ended
    uint256 blockNumber = block.number + 20;
    vm.roll(blockNumber);
    assertEq(sophonFarming.isFarmingEnded(), true);
    assertEq(sophonFarming.isWithdrawPeriodEnded(), true);

    // Update all pools and aggregate all pools' accPointsPerShare (totalAccPointsPerShare1)
    sophonFarming.massUpdatePools();

    SophonFarmingState.PoolInfo[] memory PoolInfo;
    PoolInfo = sophonFarming.getPoolInfo();

    uint256 totalAccPointsPerShare1;
    for (uint256 i = 0; i < PoolInfo.length; i++) {
        assertEq(PoolInfo[i].lastRewardBlock, blockNumber);
        totalAccPointsPerShare1 += PoolInfo[i].accPointsPerShare;
    }

    // Restart farming by setting the endBlock == 0
    sophonFarming.setEndBlock(0, 0);

    blockNumber = block.number + 20;
    vm.roll(blockNumber);

    assertEq(sophonFarming.isFarmingEnded(), false); // Farming has restarted
    assertEq(sophonFarming.isWithdrawPeriodEnded(), false);

    // Update all pools and aggregate all pools' accPointsPerShare (totalAccPointsPerShare2)
    sophonFarming.massUpdatePools();

    PoolInfo = sophonFarming.getPoolInfo();

    uint256 totalAccPointsPerShare2;
    for (uint256 i = 0; i < PoolInfo.length; i++) {
        assertEq(PoolInfo[i].lastRewardBlock, blockNumber);
        totalAccPointsPerShare2 += PoolInfo[i].accPointsPerShare;
    }

    // More points have been incorrectly distributed (this breaks the protocol's core invariant)
    assertGt(totalAccPointsPerShare2, totalAccPointsPerShare1);
}
```

## Impact

The `setEndBlock()` can mistakenly allow the ended farming to restart. As a result, the deposited tokens are unintentionally allowed to be withdrawn. Moreover, the restarted farming also continues to distribute points/rewards to users staking in pools incorrectly.

Thus, this vulnerability obviously breaks the protocol's core invariant.

## Code Snippet

- https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L297-L300

- https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L235

- https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L248

- https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L423

- https://github.com/sherlock-audit/2024-05-sophon/blob/05059e53755f24ae9e3a3bb2996de15df0289a6c/farming-contracts/contracts/farm/SophonFarming.sol#L341-L343

## Tool used

Manual Review

## Recommendation

Add a check to verify that farming must be active before resetting the `endBlock` state variable to 0, like the snippet below.

```diff
    function setEndBlock(uint256 _endBlock, uint256 _withdrawalBlocks) public onlyOwner {
        uint256 _endBlockForWithdrawals;
        if (_endBlock != 0) {
            if (_endBlock <= startBlock || getBlockNumber() > _endBlock) {
                revert InvalidEndBlock();
            }
            if (isFarmingEnded()) {
                revert FarmingIsEnded();
            }
            _endBlockForWithdrawals = _endBlock + _withdrawalBlocks;
        } else {
+           if (isFarmingEnded()) {
+               revert FarmingIsEnded();
+           }

            // withdrawal blocks needs an endBlock
            _endBlockForWithdrawals = 0;
        }
        massUpdatePools();
        endBlock = _endBlock;
        endBlockForWithdrawals = _endBlockForWithdrawals;
    }
```