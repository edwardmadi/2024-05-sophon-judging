Witty Bubblegum Leopard

medium

# Setting a withdrawal limit will make tokens remain locked forever in the contract if users don’t withdraw on time, or will lead to protocol inconsistencies

## Summary
If the withdraw period ends, all the remaining tokens that have not been withdrawn will be kept in the contract forever, or inconsistencies will be found so that the new withdraw end timestamp can be modified, breaking contract functionality.

## Vulnerability Detail

When users want to withdraw from the farming contract, they have a maximum timestamp to do so. This timestamp is checked by the `isWithdrawPeriodEnded` when withdrawing:

```solidity
// File: SophonFarming.sol
function withdraw(uint256 _pid, uint256 _withdrawAmount) external {
        if (isWithdrawPeriodEnded()) {
            revert WithdrawNotAllowed();
        }
        ...
        
}

function isWithdrawPeriodEnded() public view returns (bool) {
        uint256 _endBlockForWithdrawals = endBlockForWithdrawals;
        if (_endBlockForWithdrawals != 0 && getBlockNumber() > _endBlockForWithdrawals) {
            return true;
        } else {
            return false;
        }
    }
```

As we can see, if the current `block.number` (`getBlockNumber()`) is greater than the configured `endBlockForWithdrawals`, the withdrawing period is ended and no more withdrawals can be performed.

Also, note how **the only way to update `endBlockForWithdrawals`** once the withdraw period has ended is by **first setting it to 0 (**given that the `isFarmingEnded` check will be performed, so if we directly set end block to a value different than 0 the function will revert), and then calling `setEndBlock` again to set it to the actual desired new value:

```solidity
// File: SophonFarming.sol

function setEndBlock(uint256 _endBlock, uint256 _withdrawalBlocks) public onlyOwner {
        uint256 _endBlockForWithdrawals;
        if (_endBlock != 0) {
            if (_endBlock <= startBlock || getBlockNumber() > _endBlock) {
                revert InvalidEndBlock();
            }
            if (isFarmingEnded()) {
                revert FarmingIsEnded();
            }
            _endBlockForWithdrawals = _endBlock + _withdrawalBlocks;
        } else {
            // withdrawal blocks needs an endBlock
            _endBlockForWithdrawals = 0;
        }
        massUpdatePools();
        endBlock = _endBlock;
        endBlockForWithdrawals = _endBlockForWithdrawals;
    }
    
    function isFarmingEnded() public view returns (bool) {
        uint256 _endBlock = endBlock;
        if (_endBlock != 0 && getBlockNumber() > _endBlock) {
            return true;
        } else {
            return false;
        }
    }
```

This approach is not correct, given that if the `endBlock` is firstly set to 0 in order to be able to set a new `endBlock`, the period between `endBlock` being 0 and the new `endBlock` being set allows users to deposit into the protocol, given that `isFarmingEnded` will return false because `endBlock` will be 0. This leads to two big inconsistencies in the farming contract:

- There will exist a period where theoretically farming has ended, but in which users will still be able to perform deposits, breaking the expected behavior of the protocol
- Although setting the end block at 0 and then updating it to the new desired value is a temporary solution, this does not fully fix the issue where users not withdrawing in the withdrawal period will get their tokens locked forever, given that there will always remain users that don’t withdraw on the correct period.

A fix for this could also seem to be setting the withdrawal period to an extremely big `block.number`, but this would still not be a good solution as users would then be able to obtain all their points even if they don’t withdraw on time.

This issue derives from having removed a critical function in SushiSwap’s Masterchef:

```solidity
// MasterChef.sol
function emergencyWithdraw(uint256 _pid) public {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];
        pool.lpToken.safeTransfer(address(msg.sender), user.amount);
        emit EmergencyWithdraw(msg.sender, _pid, user.amount);
        user.amount = 0;
        user.rewardDebt = 0;
    }
```

`emergencyWithdraw` allowed users that don’t withdraw on time to still obtain their funds back (although without the corresponding earned rewards), without having to modify ending periods and break core mechanics.

## Impact
Medium. Core mechanics in the farming contract will be broken, and user’s funds can potentially remain locked forever.

## Code Snippet

https://github.com/sherlock-audit/2024-05-sophon/blob/main/farming-contracts/contracts/farm/SophonFarming.sol#L700

## Tool used

Manual Review

## Recommendation
Implement an emergencyWithdraw-like function similar to SushiSwap’s, that allows users that have not performed a withdrawal before the withdrawal period ended to still obtain their funds, although without the corresponding rewards.
