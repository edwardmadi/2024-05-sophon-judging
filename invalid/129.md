Amusing Coconut Lark

medium

# Incorrectly calculated `totalAllocPoint` variable when `_withUpdate` is false

## Summary

The add() and the set() functions can only be called by the contract owner, but it is possible that the totalAllocPoint state will be changed without setting the `_withUpdate` parameter to true.

## Vulnerability Detail

The `totalAllocPoint` variable is used to determine the portion that each pool would get from the total Point, so it is one of the main factors used in the points calculation. Therefore, whenever the `totalAllocPoint` variable is modified without updating the pending points first, the reward of each pool will be incorrectly calculated.

In the add() and set() functions shown below, if `_withUpdate` is set to false, the totalAllocPoint
variable will be modified without updating the Points (massUpdatePools()).

```solidity
    function add(uint256 _allocPoint, address _lpToken, string memory _description, bool _withUpdate) public onlyOwner returns (uint256) {
        if (poolExists[_lpToken]) {
            revert PoolExists();
        }
        if (isFarmingEnded()) {
            revert FarmingIsEnded();
        }
        if (_withUpdate) {
            massUpdatePools();
        }
        uint256 lastRewardBlock =
            getBlockNumber() > startBlock ? getBlockNumber() : startBlock;
        totalAllocPoint = totalAllocPoint + _allocPoint;
        poolExists[_lpToken] = true;

        uint256 pid = poolInfo.length;

        poolInfo.push(
            PoolInfo({
                lpToken: IERC20(_lpToken),
                l2Farm: address(0),
                amount: 0,
                boostAmount: 0,
                depositAmount: 0,
                allocPoint: _allocPoint,
                lastRewardBlock: lastRewardBlock,
                accPointsPerShare: 0,
                description: _description
            })
        );

        emit Add(_lpToken, pid, _allocPoint);

        return pid;
    }

        function set(uint256 _pid, uint256 _allocPoint, bool _withUpdate) public onlyOwner {
        if (isFarmingEnded()) {
            revert FarmingIsEnded();
        }
        if (_withUpdate) {
            massUpdatePools();
        }

        PoolInfo storage pool = poolInfo[_pid];
        address lpToken = address(pool.lpToken);
        if (lpToken == address(0) || !poolExists[lpToken]) {
            revert PoolDoesNotExist();
        }
        totalAllocPoint = totalAllocPoint - pool.allocPoint + _allocPoint;
        pool.allocPoint = _allocPoint;

        if (getBlockNumber() < pool.lastRewardBlock) {
            pool.lastRewardBlock = startBlock;
        }

        emit Set(lpToken, _pid, _allocPoint);
    }
```
## Impact

whenever the totalAllocPoint variable is modified without updating the pointReward first, the reward of each pool will be incorrectly calculated.

## Code Snippet

https://github.com/sherlock-audit/2024-05-sophon/blob/main/farming-contracts/contracts/farm/SophonFarming.sol#L153-L216



## Tool used

Manual Review

## Recommendation

Inspex suggests removing the `_withUpdate` variable in the add() and set() functions and always calling
the massUpdatePools() function before updating totalAllocPoint variable.
